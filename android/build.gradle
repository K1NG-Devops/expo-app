// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath('com.android.tools.build:gradle')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
  
  configurations.all {
    resolutionStrategy {
      // Replace old support libraries with AndroidX equivalents
      dependencySubstitution {
        substitute module('com.android.support:support-compat') using module('androidx.core:core:1.13.1')
        substitute module('com.android.support:versionedparcelable') using module('androidx.versionedparcelable:versionedparcelable:1.2.0')
        substitute module('com.android.support:support-v4') using module('androidx.legacy:legacy-support-v4:1.0.0')
      }
      
      // Force specific AndroidX versions
      force 'androidx.core:core:1.13.1'
      force 'androidx.versionedparcelable:versionedparcelable:1.2.0'
    }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
